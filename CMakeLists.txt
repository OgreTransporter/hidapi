cmake_minimum_required(VERSION 3.16.0)

file(READ "configure.ac" _file_content)
string(REGEX MATCHALL "\\[HIDAPI_([A-Z]+)\\],([ ]+)([0-9]+)" _lib_version ${_file_content})
foreach(rxver ${_lib_version})
  string(REGEX MATCH "\\[HIDAPI_([A-Z]+)\\],([ ]+)([0-9]+)" _line_ver ${rxver})
  set(HIDAPI_${CMAKE_MATCH_1} ${CMAKE_MATCH_3})
endforeach()
set(VERSION_STRING "${HIDAPI_MAJOR}.${HIDAPI_MINOR}.${HIDAPI_RELEASE}")
message("Configure HIDAPI ${VERSION_STRING}")

project(HIDAPI LANGUAGES C CXX VERSION ${VERSION_STRING})
if(NOT CMAKE_DEBUG_POSTFIX)
  set(CMAKE_DEBUG_POSTFIX "d")
endif()
set(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake ${CMAKE_MODULE_PATH})

if(WIN32)
  set(HIDAPI_SRC windows/hid.c)
  set(HIDAPI_LIB setupapi.lib)
else()
  if((CMAKE_SYSTEM_NAME STREQUAL "FreeBSD") OR (CMAKE_SYSTEM_NAME STREQUAL "kFreeBSD"))
    set(HIDAPI_INTERFACE "libusb")
  elseif(APPLE)
    # TODO: Add find IOHidManager
    set(HIDAPI_SRC mac/hid.c)
  else()
    set(HIDAPI_INTERFACE "libusb" CACHE STRING "Interface chosen for use by HIDAPI")
    set_property(CACHE HIDAPI_INTERFACE PROPERTY STRINGS libusb hidraw)
  endif()
  if(HIDAPI_INTERFACE EQUAL "libusb")
    find_package(LibUSB REQUIRED)
    include_directories(${LIBUSB_INCLUDE_DIR})
    set(HIDAPI_SRC libusb/hid.c)
    set(HIDAPI_LIB ${LIBUSB_LIBRARY})
  else()
    set(HIDAPI_SRC linux/hid.c)
  endif()
endif()
set(HIDAPI_HDR hidapi/hidapi.h)

include_directories(${CMAKE_CURRENT_SOURCE_DIR}/hidapi)

add_library(hidapi SHARED ${HIDAPI_SRC} ${HIDAPI_HDR})
target_link_libraries(hidapi PUBLIC ${HIDAPI_LIB})
target_compile_definitions(hidapi PUBLIC -DHIDAPI_EXPORTS)

add_library(libhidapi STATIC ${HIDAPI_SRC} ${HIDAPI_HDR})
target_link_libraries(hidapi PUBLIC ${HIDAPI_LIB})
target_compile_definitions(libhidapi PUBLIC -DHIDAPI_STATIC)

install(TARGETS hidapi libhidapi)
install(FILES hidapi/hidapi.h TYPE INCLUDE)

option(BUILD_TESTS "Build test programms" ON)
if(BUILD_TESTS)
  find_package(FOX)
  if(FOX_FOUND)
    add_executable(hidapi-testgui ${HIDAPI_SRC} ${HIDAPI_HDR} testgui/test.cpp)
    target_compile_definitions(hidapi-testgui PUBLIC -DHIDAPI_STATIC)
	target_include_directories(hidapi-testgui PUBLIC ${FOX_INCLUDE_DIR})
    target_link_libraries(hidapi-testgui PUBLIC ${HIDAPI_LIB} ${FOX_LIBRARY})
    install(TARGETS hidapi-testgui)
  endif()
  add_executable(hidtest ${HIDAPI_SRC} ${HIDAPI_HDR} hidtest/test.c)
  target_link_libraries(hidtest PUBLIC ${HIDAPI_LIB})
  target_compile_definitions(hidtest PUBLIC -DHIDAPI_STATIC)
  install(TARGETS hidtest)
endif()

option(BUILD_DOCUMENTATION "Create and install the HTML based API documentation (requires Doxygen)" OFF)
find_package(Doxygen)

if(BUILD_DOCUMENTATION)
  if(NOT DOXYGEN_FOUND)
    message(FATAL_ERROR "Doxygen is needed to build the documentation.")
  endif()
  if(DOXYGEN_DOT_FOUND)
    set(HAVE_DOT "YES")
  else()
    set(HAVE_DOT "NO")
  endif()
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/cmake/Doxyfile.in ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile @ONLY)
  add_custom_target(doc_doxygen ALL
    COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM)
  install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/html TYPE DOC)
endif()